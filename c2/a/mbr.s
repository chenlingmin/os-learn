;主引导程序
;------------------------------------------------------------------
SECTION MBR vstart=0x7c00
    mov ax, cs
    mov ds, ax
    mov es, ax
    mov fs, ax
    mov sp, 0x7c00

; 清屏 利用0x06号功能，上卷全部行，则可清屏。
;------------------------------------------------------------------
;INT 0x10   功能号:0x06	   功能描述:上卷窗口
;------------------------------------------------------------------
;输入：
;AH 功能号= 0x06
;AL = 上卷的行数(如果为0,表示全部)
;BH = 上卷行属性
;(CL,CH) = 窗口左上角的(X,Y)位置
;(DL,DH) = 窗口右下角的(X,Y)位置
;无返回值：

    mov ax, 0x0600
    mov bx, 0x0700
    mov cx, 0               ;左上角: (0, 0)
    mov dx, 0x184f          ;右下角: (80, 25)

    int 0x10

;下面三行代码是获取光标位置
    mov ah, 3               ;输入：3号子功能是获取光标位置
    mov bh, 0               ;bh寄存器存储的是待获取光标的页号

    int 0x10                ;输出: ch=光标开始行，cl光标结束行
                            ;     dh=光标所在行号，dl=光标所在列号

;打印字符串
    mov ax, message
    mov bp, ax              ;es:bp, es与cs一致，开头时已初始化
    mov cx, 5               ;cx 为串长度，不包含结束符0的字符个数
    mov ax, 0x1301          ;子功能13号是显示字符及属性，需要存入 ah 寄存器
                            ;al设置写字符方式 al=01:显示字符串，光标跟随移动
    mov bx, 0x0002          ;bh存储要显示的页号，此处是第0页
                            ;bl是字符属性，黑底绿字 bl=02h
    int 0x10
    jmp $

    message db "1 MBR"
    times 510-($-$$) db 0
    db 0x55, 0xaa